options{
	STATIC=false;
	UNICODE_INPUT=true;
	MULTI=true;
	VISITOR=true;
	JDK_VERSION="1.6";
	NODE_PREFIX="QueryToken";
	NODE_CLASS="MyNode";
	TRACK_TOKENS=true;
}

PARSER_BEGIN(FilterParser)
package jp.mydns.turenar.twclient.filter.tokenizer;

public class FilterParser{
	public static void main(String[] args) throws ParseException {
		FilterParser parser = new FilterParser(System.in);
		parser.enable_tracing();
		parser.Start();
	}
}
PARSER_END(FilterParser)

SKIP:
{
	" " | "\r" | "\t" | "\n"
}

TOKEN:
{
	< #OPERATOR_CHAR: [":", "!", "=", "<", ">", "~", "?"] >
	| < OPERATOR: (<OPERATOR_CHAR>)+ >
	| < LPARAN: "(" >
	| < RPARAN: ")" >
	| < COMMA: "," >
	| < INT_LITERAL: ( ["1"-"9"] (["0"-"9"])* | "0" ) >
	| < BOOL_LITERAL: ( "false" | "true" ) >
	| < STRING_LITERAL: "\""
		( (~["\"","\\","\n","\r"]) | ("\\" ["n","\\","'","\""] ) )*
			"\"" >
	| < #ALPHACHAR: ["a"-"z","A"-"Z","_"] >
	| < ALPHASTR: (<ALPHACHAR>)+ >
}

SPECIAL_TOKEN:
{
	< COMMENT: (
		"/*" // start of comment
			( ~["*"] // not astarisk
			| (
				("*")+
				(~["*","/"]) // */
			))*
		("*")+ "/" ) >
}

QueryTokenStart Start() #Start: {}
{
	(Query())? EndOfData()
	{ return jjtThis; }
}

void EndOfData(): {}
{
	<EOF>
}

void Query(): {}
{
	(
		( LOOKAHEAD(2) Function() | Property() )
	)
}

void Property(): { Token t; }
{
	t=<ALPHASTR> { jjtThis.jjtSetValue(t.image); }
	(
		( PropertyOperator()
			(PropertyValue())?
		)?
	)
}

void PropertyOperator(): { Token t; }
{
	t=<OPERATOR> { jjtThis.jjtSetValue(t.image); }
}

void PropertyValue(): { Token t; }
{
	(t=<INT_LITERAL> | t=<BOOL_LITERAL> | t=<STRING_LITERAL>) { jjtThis.jjtSetValue(t.image); }
}

void Function(): { Token t; }
{
	t=<ALPHASTR> { jjtThis.jjtSetValue(t.image); }
	FunctionLeftParenthesis() (
		( LOOKAHEAD(2) Function() | Property() )
		(
			FunctionArgSeparator()
			(LOOKAHEAD(2) Function() | Property() )
		)*
	)? FunctionRightParenthesis()
}

void FunctionArgSeparator(): {}
{
	","
}

void FunctionLeftParenthesis(): {}
{
	"("
}

void FunctionRightParenthesis(): {}
{
	")"
}
